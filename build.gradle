plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
}

group = 'data.dynamodb.repository'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()

    maven {
        name "DynamoDB Local Release Repository - South America (SÃ£o Paulo) Region"
        url "https://s3-sa-east-1.amazonaws.com/dynamodb-local-sao-paulo"
    }
}

dependencies {
    implementation 'com.amazonaws:aws-java-sdk-dynamodb:1.12.606'
    implementation 'com.google.guava:guava:32.1.3-jre'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'


    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation 'org.mockito:mockito-core:5.8.0'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'software.amazon.awssdk:url-connection-client:2.21.40'

    testImplementation 'com.amazonaws:DynamoDBLocal:2.1.0'

}

test {
    useJUnitPlatform()
}

task copyNativeDeps(type: Copy) {
    from(configurations.runtimeClasspath) {
        include '*.dll'
        include '*.dylib'
        include '*.so'
    }
    into 'build/libs'
}

test {
    dependsOn copyNativeDeps
    systemProperty 'java.library.path', 'build/libs'
    finalizedBy jacocoTestReport
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/dynamodb-java-repository/dynamodb-java-repository")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}